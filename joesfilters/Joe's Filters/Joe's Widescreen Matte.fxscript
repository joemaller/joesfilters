generator "Joe's Widescreen Matte RT"
group "Joe's Generators [BETA]"

input l0, "Matte Settings", label, "";

input p1, "Top Left Corner", point, -0.5, -0.27366666666666667
input p2, "Bottom Right Corner", point, 0.5, 0.27366666666666667;
input sides, "Top/Bottom Only", checkbox, 1;
input mattecolor, "Matte Color", color, 255, 0, 0, 0;
input matteopacity, "Matte Opacity", slider, 100, 0, 100;

input l1, "Border Settings", label, ""

input framer, "Border On/Off", checkbox, 0;
input framecolor, "Border Color", color, 255, 160, 160, 200;
input framewidth, "Border Width", slider, 2, 1, 20;

ProducesAlpha
AlphaType(kalpha);
InformationFlag("YUVaware");


code


// INCLUDE "RGBtoYUVcolor.fxscript"  vNew

on RGBtoYUVcolor(color _rgbColor)
	color _temp;
	_temp = _rgbColor
	_rgbColor.r = _temp.r * 0.299 + _temp.g * 0.587 + _temp.b * 0.114 
	_rgbColor.g = _temp.r * -0.169 + _temp.g * -0.332 + _temp.b * 0.500 + 128; 
	_rgbColor.b = _temp.r* 0.500 + _temp.g * -0.419 + _temp.b * -0.0813 + 128;
end 
// END "RGBtoYUVcolor.fxscript"
 




exposedbackground = 1;

float w,h;
point outrect[4];
DimensionsOf(dest, w, h)
image blank[2][2];


if (getPixelFormat(dest) == kFormatYUV219)
	RGBtoYUVcolor(mattecolor);
end if

channelFill(blank, 0, mattecolor.r, mattecolor.g, mattecolor.b);
channelFill(dest, matteopacity/100 * 255, mattecolor.r, mattecolor.g, mattecolor.b);

if (!sides)
	makeRect(outrect, p1.x * w, p1.y * h, p2.x * w - p1.x * w, p2.y * h - p1.y * h);
else
	makeRect(outrect, -w/2 - 50, p1.y * h, w + 100, p2.y * h - p1.y * h);
end if

BlitRect(blank, outrect, dest, outrect);


if framer
	framePoly(outrect, dest, framecolor, framewidth);
end if









